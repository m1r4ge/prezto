#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sviatoslav Grebenchukov <m1r4gexx@gmail.com>
#
# Screenshots:
#

# Load dependencies.
pmodload 'helper'

function prompt_m1r4ge_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_m1r4ge_pwd="$MATCH"
    unset MATCH
  else
    _prompt_m1r4ge_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_m1r4ge_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_m1r4ge_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_m1r4ge_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_m1r4ge_precmd

  # Set editor-info parameters.
#  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ''
  #zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{yellow}'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{red}'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{blue}%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%F{red}✱%f'
  zstyle ':prezto:module:git:info:keys' format 'prompt' ' (%b%D)' 

  # Define prompts.
  PROMPT='%F{green}${_prompt_m1r4ge_pwd}%f${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} $ '
  #RPROMPT='${editor_info[overwrite]}%(?:: asdf)${VIM:+" %F{green}V%f"}'
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${INSIDE_EMACS:+" %B%F{green}E%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_m1r4ge_setup "$@"
